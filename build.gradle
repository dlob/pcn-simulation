plugins {
    id 'application'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'com.github.ben-manes.versions' version '0.22.0'
}

group 'au.csiro.data61.pcnsimulation'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2'
    compile 'com.google.guava:guava:28.0-jre'
    compile 'io.javalin:javalin:3.4.1'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'org.jgrapht:jgrapht-core:1.3.1'
    compile 'org.slf4j:slf4j-simple:1.7.27'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'info.picocli:picocli:4.0.2'
    testCompile 'junit:junit:4.12'
}

sourceSets {
    main {
        resources { // remove unnecessary artifacts from resources
            include 'simplelogger.properties'
            include 'public/index.html'
            include 'public/dist/**'
            exclude 'public/dist/*.map'
        }
    }
}

jar {
    manifest {
        attributes "Main-Class": "au.csiro.data61.pcnsimulation.MainKt"
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } // fat JAR
}

test {
    testLogging {
        showStandardStreams = true
    }
}

application {
    mainClassName = 'au.csiro.data61.pcnsimulation.MainKt'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
    kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi']
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
